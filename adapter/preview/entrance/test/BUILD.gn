# Copyright (c) 2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//foundation/arkui/ace_engine/ace_config.gni")
import(
    "//foundation/arkui/ace_engine/adapter/preview/build/config_js_engine.gni")
import("//foundation/arkui/ace_engine/interfaces/napi/kits/napi_lib.gni")

config("bridge_config") {
  cflags_cc = [
    "-std=c++17",
    "-Wno-c99-designator",
    "-Wno-unknown-warning-option",
  ]
  include_dirs = [
    "$ace_root",
    "$ace_root/frameworks",
    "$flutter_root/engine",
    "$flutter_root/engine/flutter/shell/platform/common/cpp/public",
    "$flutter_root/engine/ace_adapter/flutter/shell/platform/glfw/public",
  ]
}

template("test_bridge") {
  forward_variables_from(invoker, "*")

  ohos_shared_library(target_name) {
    configs = [ ":bridge_config" ]

    sources = [
      "run_args_parser.cpp",
      "test_bridge.cpp",
    ]

    deps = [
      "$ace_napi:ace_napi(${current_toolchain})",
      "$ace_root/adapter/preview/build:libace_engine_$platform(${current_toolchain})",
    ]
    if (platform == "windows") {
      libs = [ "pthread" ]
    }
  }
}

test_bridge("test_bridge_windows") {
  platform = "windows"
}

test_bridge("test_bridge_mac") {
  platform = "mac"
}
